_ar = "arm-none-eabi-ar"
_cc = "arm-none-eabi-gcc"
_cxx = "arm-none-eabi-g++"

_common_flags = "-mcpu=cortex-m0plus -mfloat-abi=soft -mthumb -mabi=aapcs"

toolchain("cortex-m0") {
  tool("cc") {
    depfile = "{{output}}.d"
    command = "${_cc} -MMD -MF $depfile {{defines}} {{include_dirs}} {{cflags}} {{cflags_c}} -c {{source}} -o {{output}} ${_common_flags}"
    depsformat = "gcc"
    description = "CC {{output}}"
    outputs =
        [ "{{source_out_dir}}/{{target_output_name}}.{{source_name_part}}.o" ]
  }

  tool("cxx") {
    depfile = "{{output}}.d"
    command = "${_cxx}  -MMD -MF $depfile {{defines}} {{include_dirs}} {{cflags}} {{cflags_cc}} -c {{source}} -o {{output}}  ${_common_flags}"
    depsformat = "gcc"
    description = "CXX {{output}}"
    outputs =
        [ "{{source_out_dir}}/{{target_output_name}}.{{source_name_part}}.o" ]
  }

  tool("alink") {
    rspfile = "{{output}}.rsp"
    rspfile_content = "{{inputs}}"
    command =
        "${_ar} rcs {{output}} @${rspfile}"
    description = "AR {{target_output_name}}{{output_extension}}"
    default_output_extension = ".a"
    outputs =
        [ "{{target_out_dir}}/{{target_output_name}}{{output_extension}}" ]
  }

  tool("link") {
    outfile = "{{root_out_dir}}/{{target_output_name}}{{output_extension}}"
    mapfile = "{{root_out_dir}}/{{target_output_name}}.map"

    rspfile = "$outfile.rsp"
    rspfile_content = "{{inputs}} {{libs}}"

    command = "${_cxx}  {{ldflags}} -Wl,--gc-sections -o $outfile -Wl,-Map=${mapfile} -Wl,--start-group @$rspfile -Wl,--end-group --specs=nosys.specs -lnosys ${_common_flags}"

    description = "LINK $outfile"
    default_output_dir = "{{root_out_dir}}"
    outputs = [ outfile ]
  }

  if (host_os == "win") {
    tool("stamp") {
      command = "cmd /c type nul > \"{{output}}\""
      description = "STAMP {{output}}"
    }

    tool("copy") {
      # TODO: win compatible copy tool
      command = "cp -af {{source}} {{output}}"
      description = "COPY {{source}} {{output}}"
    }
  } else {
    tool("stamp") {
      command = "touch {{output}}"
      description = "STAMP {{output}}"
    }

    tool("copy") {
      command = "cp -af {{source}} {{output}}"
      description = "COPY {{source}} {{output}}"
    }
  }
}
